# Jivetalking - Presenter Integration Tests
# This file contains commands for testing with local audio files (not in git)
# Path to testdata directory (source_directory returns the directory of THIS file)

testdata := source_directory()

# Test episode number (change this to test different episodes)

episode := "68"

# Get Mark's processed logs
mark-logs:
    @cat {{ testdata }}/LMP-{{ episode }}-mark-processed.log

# Stash Mark's processed logs
mark-log-stash:
    @cp {{ testdata }}/LMP-{{ episode }}-mark-processed.log {{ testdata }}/LMP-{{ episode }}-mark-stashed.log

# Process mark audio
mark:
    @rm -f {{ testdata }}/LMP-{{ episode }}-mark-processed.*
    @./jivetalking --debug --logs {{ testdata }}/LMP-{{ episode }}-mark.flac
    @just mark-logs

# Get Martin's processed logs
martin-logs:
    @cat {{ testdata }}/LMP-{{ episode }}-martin-processed.log

# Stash Martin's processed logs
martin-log-stash:
    @cp {{ testdata }}/LMP-{{ episode }}-martin-processed.log {{ testdata }}/LMP-{{ episode }}-martin-stashed.log

# Process martin audio
martin:
    @rm -f {{ testdata }}/LMP-{{ episode }}-martin-processed.*
    @./jivetalking --debug --logs {{ testdata }}/LMP-{{ episode }}-martin.flac
    @just martin-logs

# Get popey's processed logs
popey-logs:
    @cat {{ testdata }}/LMP-{{ episode }}-popey-processed.log

# Stash popey's processed logs
popey-log-stash:
    @cp {{ testdata }}/LMP-{{ episode }}-popey-processed.log {{ testdata }}/LMP-{{ episode }}-popey-stashed.log

# Process popey audio
popey:
    @rm -f {{ testdata }}/LMP-{{ episode }}-popey-processed.*
    @./jivetalking --debug --logs {{ testdata }}/LMP-{{ episode }}-popey.flac
    @just popey-logs

# Process all presenters
presenters: mark martin popey

# Process all presenters for episodes 70, 71, and 72
all-episodes:
    #!/usr/bin/env bash
    for ep in 68 69 70 71 72 73 74; do
        echo "=== Processing Episode $ep ==="
        for presenter in mark martin popey; do
            echo "--- $presenter ---"
            rm -f {{ testdata }}/LMP-${ep}-${presenter}-processed.*
            ./jivetalking --debug --logs {{ testdata }}/LMP-${ep}-${presenter}.flac
        done
    done

# Run analysis-only mode on all episodes and presenters
all-analyse:
    #!/usr/bin/env bash
    for ep in 68 69 70 71 72 73 74; do
        echo "=== Analysing Episode $ep ==="
        for presenter in mark martin popey; do
            audio_file="{{ testdata }}/LMP-${ep}-${presenter}.flac"
            log_file="{{ testdata }}/LMP-${ep}-${presenter}-analysis.log"
            if [[ -f "$audio_file" ]]; then
                echo "--- $presenter ---"
                ./jivetalking -a "$audio_file" | tee "$log_file"
            else
                echo "--- $presenter (skipped: file not found) ---"
            fi
        done
    done

# Stash all presenters logs
stash: mark-log-stash martin-log-stash popey-log-stash


# Run analysis-only mode on all episodes and presenters
stash-analysis:
    #!/usr/bin/env bash
    for ep in 68 69 70 71 72 73 74; do
        for presenter in mark martin popey; do
            log_file="{{ testdata }}/LMP-${ep}-${presenter}-analysis.log"
            stash_log_file="{{ testdata }}/LMP-${ep}-${presenter}-analysis-before.log"
            if [ -e "${log_file}" ]; then
                cp "${log_file}" "${stash_log_file}"
            fi
        done
    done
